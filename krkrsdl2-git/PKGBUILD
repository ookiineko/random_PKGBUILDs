# Maintainer: Ookiineko <chiisaineko@protonmail.com>
# Contributor: fuzzbop <fuzzbop@gmail.com>

pkgname=krkrsdl2-git
pkgver=20230613.g893b590
pkgrel=1
pkgdesc="cross-platform Kirikiri Z port based on SDL2"
arch=('arm' 'aarch64' 'i686' 'x86_64')
url="https://github.com/krkrsdl2/krkrsdl2"
license=('MIT')
depends=('sdl2')
makedepends=('git' 'cmake')
conflicts=('krkrsdl2')
provides=('krkrsdl2')
source=('git+https://github.com/krkrsdl2/krkrsdl2-env-example.git'
	'clang.patch')
md5sums=('SKIP'
         'e7a467463cbf448c3a7ac9c8ff8e6a0d')

pkgver() {
    # Update version
    cd krkrsdl2-env-example
    echo "$(git show -s --format="%ci"|grep -oP '\d{4}-\d{2}-\d{2}'|sed 's:-::g').g$(git describe --always)"
}

prepare() {
    cd "${srcdir}/krkrsdl2-env-example"
    patch -p1 < "${srcdir}/clang.patch"
}

build() {
    cmake -B build -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX="/usr" -S krkrsdl2-env-example
    cmake --build build -j $(nproc --all)
}

package() {
    install -Dm755 "${srcdir}/build/krkrsdl2_repo/${pkgname::-4}" "${pkgdir}/usr/bin/${pkgname::-4}"
}
